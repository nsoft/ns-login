/*
 *    Copyright (c) 2019, Needham Software LLC
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

plugins {
    id 'java'
    id 'war'
}

group 'com.needhamsoftware'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

dependencies {
    compile project(':core')
}

war {
  setArchiveFileName 'admin.war'
}

task ngBuild(type:Exec) {
  workingDir "$projectDir"
  commandLine 'ng', 'build'
}

task ngCopyIndex(type: Copy, dependsOn: ngBuild) {
  from file('dist/admin/wip.html')
  into file('src/main/webapp/')
  rename { String filename -> 'index.html'}
}
task ngCopy(type: Copy, dependsOn: ngCopyIndex) {
  from ("$projectDir/dist/admin") {
    exclude "wip.html"
  }

  into file('src/main/webapp/')
}

clean {
  delete fileTree('src/main/webapp') {
    include '*.js.map'
    include '*-es5.js'
    include '*-es2015.js'
    include 'index.html'
  }
  delete "$projectDir/dist"
}

build.dependsOn(ngCopy)
